// Pour thomas
// grammar game.Game with org.eclipse.xtext.common.Terminals
// generate game "http://www.Game.game"

grammar idmprojet.game.Game with org.eclipse.xtext.common.Terminals

generate game "http://www.game.idmprojet/Game"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Game:
	'Game'
	name=EString
	'{'
		'explorateur' explorateur=Explorateur
		'lieuDepart' lieuDepart = [Lieu]
		('lieuxArrivee' '(' (lieuxArrivee += [Lieu])+')')
		('gameElements' '{' (gameElements+=GameElement)+ '}' )?
	'}' ;

EntiteLieu:
	'{'
		(entite = Personne | PackObjets | ConnaissanceLieu)
		('conditionVisibilite' conditionVisibilite=Condition)?
	'}'
;

ConnaissanceLieu:
	connaissance=[Connaissance]
;

GameElement:
	Lieu | Chemin | Objet | Connaissance ;

EString returns ecore::EString:
	STRING | ID;

EBoolean returns ecore::EBoolean:
	'true' | 'false';

Explorateur:
	name=EString
	'{'
		'tailleInventaire' tailleInventaire=INT
		('connaissances' '(' (connaissances+=[Connaissance])+')')?
		('inventaire' '(' (inventaire+=PackObjets)+')')?
	'}'
;

Personne :
	'personne'
	name=EString
	interaction=Interaction
;
Lieu : 
		'lieu'
	name = EString 
	'{'
		'description' description = Description
		('cheminsPossibles' '(' (cheminsPossibles+=[Chemin])+')')?
		('entites' '{'(entiteLieu+=EntiteLieu)+ '}')?
	'}';


Objet :
		'objet'
	name=EString
	'{'
		'description' description = Description
		'taille' taille=INT
	'}'
;

PackObjets :
	'{'
		objet = [Objet]
		',' quantite = INT
	'}'
;

Chemin :
	'chemin'
	name=EString
	'{'
		'description' description = Description
		('conditionVisibilite' conditionVisibilite=Condition)?
		'lieu1' lieu1 = [Lieu]
		'lieu2' lieu2 = [Lieu]
		('ouvert' ouvert = Condition)?
		('recompenses' '('(recompenses+=Recompense)+ ')')?
		('consommations' '('(consommations+=PackObjets)+ ')')?
	'}'
;

Connaissance :
	'connaissance'
	name=EString
	'{'
		'description' description = Description
	'}'
;

Condition :
	(('(' conjonctions+=Conjonction('|' conjonctions+=Conjonction)* ')') | {Condition} EBoolean)
;

OperateurComparaison :
	'<' | '=' | '>'
;

Conjonction :
	'(' litteraux+=Litteral ('&' litteraux+=Litteral)* ')'
;

Litteral :
	'('objet = [Objet] operateur = OperateurComparaison quantite = INT')'
   |'('connaissance = [Connaissance] '=' EBoolean ')'
;


Description :
	'{'
		(textes+=Texte)+
	'}'
;

Texte :
	'{'
		contenu = EString
		('condition' condition = Condition)?
	'}' 
;

Recompense :
	'{'
		(connaissanceObtenue = [Connaissance] | objetsObtenus = PackObjets)
		('conditionObtention' conditionObtention = Condition)?
	'}'
;

Action :
	'action'
	'{'
		'description' description = Description
		('condition' condition=Condition)?
		('recompenses' '('(recompenses+=Recompense)+ ')')?
		('consommations' '('(consommations+=PackObjets)+ ')')?
		'choixSuivant' choixSuivant=[Choix]
	'}';
	
Interaction :
	'interaction'
	'{'
		('recompenses' '(' (recompenses+=Recompense)+ ')' )?
		(('listeChoixDebut' '(' (listeChoixDebut+=[Choix] listeConditionsDebut+=Condition)+')') | 'seulChoixDebut' seulChoixDebut = [Choix])
		'listeChoix' '(' (listeChoix+=Choix)+')'
	'}'
;

Choix :
	'choix'
	name=EString
	'{'
		'description' description = Description
		('choixFin' choixFin = Condition)?
		('listeActions' '(' (listeActions+=Action+)')')?
	'}'	
;